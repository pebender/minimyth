diff -Naur udev-150-old/configure.ac udev-150-new/configure.ac
--- udev-150-old/configure.ac	2010-01-07 08:15:22.000000000 -0800
+++ udev-150-new/configure.ac	2010-01-20 09:47:44.000000000 -0800
@@ -98,6 +98,13 @@
 fi
 AM_CONDITIONAL([ENABLE_INTROSPECTION], [test "x$enable_introspection" = xyes])
 
+dnl Determine the absolute path to the header file linux/input.h.
+AX_ABSOLUTE_HEADER(linux/input.h)
+if test "x$gl_cv_absolute_linux_input_h" = "x" ; then
+    AC_MSG_ERROR([failed to find the absolute path to header file linux/input.h])
+fi
+AC_SUBST(ABSOLUTE_LINUX_INPUT_H, $gl_cv_absolute_linux_input_h)
+
 AC_CONFIG_HEADERS(config.h)
 AC_CONFIG_FILES([
 	Makefile
diff -Naur udev-150-old/m4/ax_absolute_header.m4 udev-150-new/m4/ax_absolute_header.m4
--- udev-150-old/m4/ax_absolute_header.m4	1969-12-31 16:00:00.000000000 -0800
+++ udev-150-new/m4/ax_absolute_header.m4	2010-01-20 09:46:30.000000000 -0800
@@ -0,0 +1,68 @@
+# ===========================================================================
+#       http://www.nongnu.org/autoconf-archive/ax_absolute_header.html
+# ===========================================================================
+#
+# SYNOPSIS
+#
+#   AX_ABSOLUTE_HEADER(HEADER1 HEADER2 ...)
+#
+# DESCRIPTION
+#
+#   Find the absolute name of a header file, assuming the header exists. If
+#   the header were sys/inttypes.h, this macro would define
+#   ABSOLUTE_SYS_INTTYPES_H to the `""' quoted absolute name of
+#   sys/inttypes.h in config.h (e.g. `#define ABSOLUTE_SYS_INTTYPES_H
+#   "///usr/include/sys/inttypes.h"'). The three "///" are to pacify Sun C
+#   5.8, which otherwise would say "warning: #include of /usr/include/...
+#   may be non-portable". Use `""', not `<>', so that the /// cannot be
+#   confused with a C99 comment.
+#
+# LICENSE
+#
+#   Copyright (c) 2009 Derek Price
+#   Copyright (c) 2009 Rhys Ulerich <rhys.ulerich@gmail.com>
+#
+#   Copying and distribution of this file, with or without modification, are
+#   permitted in any medium without royalty provided the copyright notice
+#   and this notice are preserved.
+
+dnl Copyright (C) 2006, 2007 Free Software Foundation, Inc.
+dnl This file is free software; the Free Software Foundation
+dnl gives unlimited permission to copy and/or distribute it,
+dnl with or without modifications, as long as this notice is preserved.
+
+dnl From Derek Price.
+dnl Modified by Rhys Ulerich to use AC_CHECK_HEADERS instead of _ONCE
+
+AU_ALIAS([GL_TRILINOS_ABSOLUTE_HEADER], [AX_ABSOLUTE_HEADER])
+AC_DEFUN([AX_ABSOLUTE_HEADER],
+[AC_LANG_PREPROC_REQUIRE()dnl
+AC_FOREACH([gl_HEADER_NAME], [$1],
+  [AS_VAR_PUSHDEF([gl_absolute_header],
+                  [gl_cv_absolute_]m4_quote(m4_defn([gl_HEADER_NAME])))dnl
+  AC_CACHE_CHECK([absolute name of <]m4_quote(m4_defn([gl_HEADER_NAME]))[>],
+    m4_quote(m4_defn([gl_absolute_header])),
+    [AS_VAR_PUSHDEF([ac_header_exists],
+                    [ac_cv_header_]m4_quote(m4_defn([gl_HEADER_NAME])))dnl
+    AC_CHECK_HEADERS(m4_quote(m4_defn([gl_HEADER_NAME])))dnl
+    if test AS_VAR_GET(ac_header_exists) = yes; then
+      AC_LANG_CONFTEST([AC_LANG_SOURCE([[#include <]]m4_dquote(m4_defn([gl_HEADER_NAME]))[[>]])])
+dnl eval is necessary to expand ac_cpp.
+dnl Ultrix and Pyramid sh refuse to redirect output of eval, so use subshell.
+      AS_VAR_SET(gl_absolute_header,
+[`(eval "$ac_cpp conftest.$ac_ext") 2>&AS_MESSAGE_LOG_FD |
+sed -n '\#/]m4_quote(m4_defn([gl_HEADER_NAME]))[#{
+	s#.*"\(.*/]m4_quote(m4_defn([gl_HEADER_NAME]))[\)".*#\1#
+	s#^/[^/]#//&#
+	p
+	q
+}'`])
+    fi
+    AS_VAR_POPDEF([ac_header_exists])dnl
+    ])dnl
+  AC_DEFINE_UNQUOTED(AS_TR_CPP([ABSOLUTE_]m4_quote(m4_defn([gl_HEADER_NAME]))),
+                     ["AS_VAR_GET(gl_absolute_header)"],
+                     [Define this to an absolute name of <]m4_quote(m4_defn([gl_HEADER_NAME]))[>.])
+  AS_VAR_POPDEF([gl_absolute_header])dnl
+])dnl
+])# AX_ABSOLUTE_HEADER
diff -Naur udev-150-old/Makefile.am udev-150-new/Makefile.am
--- udev-150-old/Makefile.am	2010-01-07 07:18:17.000000000 -0800
+++ udev-150-new/Makefile.am	2010-01-20 09:51:00.000000000 -0800
@@ -546,7 +546,7 @@
 dist_udevkeymapforcerel_DATA = \
 	extras/keymap/keymaps/force-release/samsung-other
 
-extras/keymap/keys.txt: /usr/include/linux/input.h
+extras/keymap/keys.txt: $(ABSOLUTE_LINUX_INPUT_H)
 	mkdir -p extras/keymap
 	$(AM_V_GEN)$(AWK) '/^#define.*KEY_[^ ]+[ \t]+[0-9]/ { if ($$2 != "KEY_MAX") { print $$2 } }' < $< | sed 's/^KEY_COFFEE$$/KEY_SCREENLOCK/' > $@
 
